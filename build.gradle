plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/magna.accessWidener")
}

repositories {
    maven { url "https://maven.draylar.dev/releases/" }
    maven { url "https://maven.jamieswhiteshirt.com/libs-release/" }
    maven { url "https://maven.shedaniel.me/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // config
    modImplementation include("dev.draylar.omega-config:omega-config-base:${project.omega_config_version}")

    // dev Reach Entity Attributes for testing tools with extended reach (or players)
    // modImplementation "com.jamieswhiteshirt:reach-entity-attributes:${project.rea_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

dependencies {
    afterEvaluate {
        testmodImplementation sourceSets.main.output
    }
}

loom {
    runs {
        testmodClient {
            client()
            name = "Test Mod Client"
            source sourceSets.testmod
        }

        testmodServer {
            server()
            name = "Test Mod Server"
            source sourceSets.testmod
        }
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
     withSourcesJar()
}

jar {
    from "LICENSE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "draylarRepository"
            url = "https://maven.draylar.dev/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}